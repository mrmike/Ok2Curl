apply plugin: 'maven-publish'
apply plugin: 'signing'

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION
                from components.java

                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = 'Ok2Curl'
                    description = 'Convert OkHttp requests into curl logs.'
                    url = 'https://github.com/mrmike/Ok2Curl'
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'mrmike'
                            name = 'Micha≈Ç Moczulski'
                            email = 'moczul@gmail.com'
                        }
                    }
                    scm {
                        connection = 'https://github.com/mrmike/Ok2Curl.git'
                        developerConnection = 'https://github.com/mrmike/Ok2Curl.git'
                        url = 'https://github.com/mrmike/Ok2Curl'
                    }
                }
            }
        }
    }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}